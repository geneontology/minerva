PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX BFO: <http://purl.obolibrary.org/obo/BFO_>
PREFIX CHEBI: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX PR: <http://purl.obolibrary.org/obo/PR_>

SELECT DISTINCT ?target ?extension_rel ?extension ?extension_type
WHERE {
# 
# ?target must be passed in VALUES block before running this query
# 
# Link between annotation target and connected individual
# seeded with someValuesFrom relations used in go-plus
VALUES ?extension_rel {
<http://purl.obolibrary.org/obo/BFO_0000050>
<http://purl.obolibrary.org/obo/BFO_0000066>
<http://purl.obolibrary.org/obo/GOCHEREL_0000001>
<http://purl.obolibrary.org/obo/GOCHEREL_0000002>
<http://purl.obolibrary.org/obo/GOCHEREL_0000003>
<http://purl.obolibrary.org/obo/GOCHEREL_0000004>
<http://purl.obolibrary.org/obo/GOREL_0000040>
<http://purl.obolibrary.org/obo/GOREL_0001004>
<http://purl.obolibrary.org/obo/GOREL_0001006>
<http://purl.obolibrary.org/obo/GOREL_0001007>
<http://purl.obolibrary.org/obo/GOREL_0002001>
<http://purl.obolibrary.org/obo/GOREL_0002002>
<http://purl.obolibrary.org/obo/GOREL_0002003>
<http://purl.obolibrary.org/obo/GOREL_0002004>
<http://purl.obolibrary.org/obo/GOREL_0002005>
<http://purl.obolibrary.org/obo/GOREL_0002006>
<http://purl.obolibrary.org/obo/GOREL_0002007>
<http://purl.obolibrary.org/obo/GOREL_0002008>
<http://purl.obolibrary.org/obo/GOREL_0012006>
<http://purl.obolibrary.org/obo/RO_0000053>
<http://purl.obolibrary.org/obo/RO_0000057>
<http://purl.obolibrary.org/obo/RO_0000087>
<http://purl.obolibrary.org/obo/RO_0001025>
<http://purl.obolibrary.org/obo/RO_0002007>
<http://purl.obolibrary.org/obo/RO_0002008>
<http://purl.obolibrary.org/obo/RO_0002087>
<http://purl.obolibrary.org/obo/RO_0002090>
<http://purl.obolibrary.org/obo/RO_0002092>
<http://purl.obolibrary.org/obo/RO_0002093>
<http://purl.obolibrary.org/obo/RO_0002160>
<http://purl.obolibrary.org/obo/RO_0002162>
<http://purl.obolibrary.org/obo/RO_0002202>
<http://purl.obolibrary.org/obo/RO_0002211>
<http://purl.obolibrary.org/obo/RO_0002212>
<http://purl.obolibrary.org/obo/RO_0002213>
<http://purl.obolibrary.org/obo/RO_0002215>
<http://purl.obolibrary.org/obo/RO_0002216>
<http://purl.obolibrary.org/obo/RO_0002220>
<http://purl.obolibrary.org/obo/RO_0002223>
<http://purl.obolibrary.org/obo/RO_0002224>
<http://purl.obolibrary.org/obo/RO_0002230>
<http://purl.obolibrary.org/obo/RO_0002231>
<http://purl.obolibrary.org/obo/RO_0002232>
<http://purl.obolibrary.org/obo/RO_0002233>
<http://purl.obolibrary.org/obo/RO_0002234>
<http://purl.obolibrary.org/obo/RO_0002263>
<http://purl.obolibrary.org/obo/RO_0002264>
<http://purl.obolibrary.org/obo/RO_0002295>
<http://purl.obolibrary.org/obo/RO_0002296>
<http://purl.obolibrary.org/obo/RO_0002297>
<http://purl.obolibrary.org/obo/RO_0002298>
<http://purl.obolibrary.org/obo/RO_0002299>
<http://purl.obolibrary.org/obo/RO_0002313>
<http://purl.obolibrary.org/obo/RO_0002315>
<http://purl.obolibrary.org/obo/RO_0002327>
<http://purl.obolibrary.org/obo/RO_0002331>
<http://purl.obolibrary.org/obo/RO_0002332>
<http://purl.obolibrary.org/obo/RO_0002333>
<http://purl.obolibrary.org/obo/RO_0002334>
<http://purl.obolibrary.org/obo/RO_0002338>
<http://purl.obolibrary.org/obo/RO_0002339>
<http://purl.obolibrary.org/obo/RO_0002340>
<http://purl.obolibrary.org/obo/RO_0002341>
<http://purl.obolibrary.org/obo/RO_0002342>
<http://purl.obolibrary.org/obo/RO_0002343>
<http://purl.obolibrary.org/obo/RO_0002344>
<http://purl.obolibrary.org/obo/RO_0002345>
<http://purl.obolibrary.org/obo/RO_0002348>
<http://purl.obolibrary.org/obo/RO_0002349>
<http://purl.obolibrary.org/obo/RO_0002355>
<http://purl.obolibrary.org/obo/RO_0002356>
<http://purl.obolibrary.org/obo/RO_0002412>
<http://purl.obolibrary.org/obo/RO_0002428>
<http://purl.obolibrary.org/obo/RO_0002429>
<http://purl.obolibrary.org/obo/RO_0002430>
<http://purl.obolibrary.org/obo/RO_0002431>
<http://purl.obolibrary.org/obo/RO_0002495>
<http://purl.obolibrary.org/obo/RO_0002565>
<http://purl.obolibrary.org/obo/RO_0002578>
<http://purl.obolibrary.org/obo/RO_0002629>
<http://purl.obolibrary.org/obo/RO_0002630>
}

?target ?extension_rel ?extension . 
# We don't want reflexive links
FILTER(?extension != ?target)
# Exclude redundant links between target and extension
FILTER NOT EXISTS {
    ?other_extension_rel rdfs:subPropertyOf ?extension_rel . 
    ?target ?other_extension_rel ?extension .
    FILTER(?other_extension_rel != ?extension_rel)
}
?extension rdf:type ?extension_type .
# Only get direct types for the extension filler
FILTER NOT EXISTS {
    ?extension <http://arachne.geneontology.org/indirect_type> ?extension_type .
}
FILTER(isIRI(?extension_type) && (?extension_type NOT IN (rdfs:Resource, owl:Thing, owl:NamedIndividual, BFO:0000002, BFO:0000003, BFO:0000004, BFO:0000015, BFO:0000040)))
FILTER(!STRSTARTS(STR(?extension_type), "http://purl.obolibrary.org/obo/CARO_"))
}
